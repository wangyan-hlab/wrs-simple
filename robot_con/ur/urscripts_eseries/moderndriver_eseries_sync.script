def driverProg():
	SERVO_IDLE = 0
	SERVO_RUNNING = 1
	cmd_servo_state = SERVO_IDLE
	cmd_servo_q = [0.0, 0.0, 0.0, 0.0, 0.0, 0.0]

	def set_servo_setpoint(q):
		enter_critical
		cmd_servo_state = SERVO_RUNNING
		cmd_servo_q = q
		exit_critical
	end

	thread servoThread():
		state = SERVO_IDLE
		while True:
			enter_critical
			q = cmd_servo_q
			do_brake = False
			if (state == SERVO_RUNNING) and (cmd_servo_state == SERVO_IDLE):
				do_brake = True
			end
			state = cmd_servo_state
			cmd_servo_state = SERVO_IDLE
			exit_critical
			if do_brake:
				stopj(1.0)
				sync()
			elif state == SERVO_RUNNING:
				# textmsg(q)
				servoj(q)
			else:
				sync()
			end
		end
	end

    def wait_running_handshake():
        write_port_register(255,1)
        modbus_add_signal("parameter_peer_ip", 0, 255, 3, "output1")
        while modbus_get_signal_status("output1", False) < 1:
            sleep(0.02)
            textmsg("No signal")
        end
        modbus_delete_signal("output1")
        textmsg("Execution")
    end

	socket_open("parameter_ip", parameter_port)
	textmsg("open connection")
    wait_running_handshake()
	thread_servo = run servoThread()
	keepalive = 1
	while keepalive > 0:
		# textmsg("preparing to read")
		# textmsg(keepalive)
		params_mult = socket_read_binary_integer(6+1)
		if params_mult[0] > 0:
			q = [params_mult[1]/parameter_jointscaler,
				 params_mult[2]/parameter_jointscaler,
				 params_mult[3]/parameter_jointscaler,
				 params_mult[4]/parameter_jointscaler,
				 params_mult[5]/parameter_jointscaler,
				 params_mult[6]/parameter_jointscaler]
			keepalive = params_mult[7]
			set_servo_setpoint(q)
			#textmsg(q)
	        sleep(.002)
		end
	end
	sleep(.1)
	socket_close()
	kill thread_servo
	write_port_register(255,0)
end
